(* TEORIA DA COMPUTA√á√ÉO 2019/2020
 *
 * Autor: Igor Nunes (N¬∫ 41381)
 * Data:  11/06/2020 (vers√£o 1)
 *
 * Recorre a um aut√≥mato minimalista.
 * 
 * Changelog:
 *    0.1 (28/mai/2020) -> Esbo√ßo do c√≥digo
 *    1.0 (17/jun/2020) -> Primeira vers√£o
 *    1.1 (04/jul/2020) -> Optimiza√ß√£o (buffering)
 *)

(* 
 * ===== PbD: Problema D =====
 * Documenta√ß√£o adicionada a 17/06/2020.
 * Bibliografia: aulas da unidade curricular e apontamentos do professor.
 * Exemplo de execu√ß√£o no final.
 *)

(* Tipo de dados:  regexp
 * ----------------------
 * Extra√≠do do c√≥digo fornecido no Problema C.
 *)
type regexp =
  | V  
  | E
  | C of char
  | U of regexp * regexp 
  | P of regexp * regexp 
  | S of regexp


(* Fun√ß√£o:  string_of_regexp
 * -------------------------
 * Produz uma string correspondente √† express√£o regular de input.
 * C√≥digo fornecido pelo professor.
 * Extra√≠do do c√≥digo fornecido no Problema C.
 *)
let rec string_of_regexp s =
  match s with
  | V        -> "0"
  | E        -> "1"
  | C  c     -> String.make 1 c    
  | U (f, g) -> "(" ^ (string_of_regexp f) ^ " + " ^ (string_of_regexp g) ^ ")"
  | P (f, g) -> "(" ^ (string_of_regexp f) ^ " . " ^ (string_of_regexp g) ^ ")"
  | S s      -> (string_of_regexp s) ^ "*"


(* Operador:  <|
 * -------------
 * Equivale ao operador . de Haskell
 * 
 * Operador:  <<
 * -------------
 * Equivale ao operador $ de Haskell
 * 
 * Fun√ß√£o:  subtract
 * -----------------
 * Equivale √† fun√ß√£o subtract de Haskell
 *)
let (<|) f g x = f (g x)
let (<<) f x   = f x
let subtract x y = y - x


(* Tipo de dados:  label
 * ---------------------
 * Representa o r√≥tulo de uma transi√ß√£o no aut√≥mato.
 * 
 * Para o r√≥tulo optei pelo tipo de dados 'label' uma vez que Null pode representar
 * directamente uma transi√ß√£o 0, Epsilon representa uma transi√ß√£o ∆ê, enquanto
 * Char c representa uma transi√ß√£o com o caracter c.
 * 
 * Criado e usado originalmente no Problema C.
 *)
type label =
  | Null
  | Epsilon
  | Char of char


(* Tipo de dados:  transition
 * --------------------------
 * Representa uma transi√ß√£o no aut√≥mato. √â um tuplo com os seguintes 2 elementos:
 *   -> Estado para o qual transita;
 *   -> R√≥tulo da transi√ß√£o (caracter, ∆ê ou 0).
 * 
 * Criado e usado originalmente no Problema C.
 *)
type transition = int * label


(* Tipo de dados:  automata
 * ------------------------
 * Representa um aut√≥mato.
 * Um aut√≥mato √© um array de listas de transi√ß√µes.
 * Cada √≠ndice do array representa um n√≥ do aut√≥mato, e este tem uma lista de
 * transi√ß√µes poss√≠veis.
 * Um estado final √© um n√≥ cuja lista de transi√ß√µes √© vazia.
 * 
 * O array ir√° permitir acesso directo, que √© mais eficiente, e permitir√°
 * simular um sistema com um "apontador" na execu√ß√£o do aut√≥mato.
 * 
 * Detalhes do uso descritos na sec√ß√£o "Exemplo de execu√ß√£o".
 * 
 * Criado e usado originalmente no Problema C.
 *)
type automata = transition list array


(* Constante:  t_null
 * ------------------
 * Lista de transi√ß√µes vazia.
 * Um n√≥ do aut√≥mato que armazene um t_null √© um estado final.
 * 
 * Criado e usado originalmente no Problema C.
 *)
let t_null : transition list = []


(* Constante:  init_automata
 * -------------------------
 * Aut√≥mato inicial.
 * 
 *   n: n√∫mero de estados do aut√≥mato.
 * 
 * Inicializa com a constante t_null.
 * 
 * Criado e usado originalmente no Problema C.
 *)
let init_automata n : automata = Array.make n t_null


(* Fun√ß√£o:  make_matrix
 * --------------------
 * Cria uma matriz max * max * max+1 que serve de buffer de mem√≥ria para o
 * Algoritmo de MacNaughton-Yamada.
 *)
let make_matrix max : regexp option array array array =
  Array.init max (fun _ -> Array.init max (fun _ -> (Array.init (max + 1) (fun _ -> None))))


(* Fun√ß√£o:  simplify
 * -----------------
 * Simplifica o aut√≥mato e aplica as regras indicadas no enunciado, nomeadamente:
 * 
 *   1. 0+r = r+0 = r
 *   2. 0.r = r.0 = 0
 *   3. 0* = ∆ê
 *   4. A+B ordano segundo 0 < ∆ê < c.
 * 
 * Aplica uma simplifica√ß√£o adicional inspirado pelo c√≥digo originalmente fornecido
 * pelo professor.
 *)
let rec simplify s =
  match s with
  | U (f, g) -> (
    let f, g = simplify f, simplify g in
    match f, g with
    | C a, C b -> if a > b then U (C b, C a) else U (C a, C b)
    | V, x
    | x, V     -> x
    | E, C a
    | C a, E   -> U (E, C a)
    | _        -> U (f, g)
  )
  | P (f, g) -> (
    let f, g = simplify f, simplify g in
    match f, g with
    | V, _
    | _, V -> V
    | _    -> P (f, g)
  )
  | S f      -> (
    let f = simplify f in
    match f with
    | E
    | V -> E
    | U (E, g)
    | U (g, E) -> S g
    | _ -> S f
  )
  | _        -> s


(* Fun√ß√µes auxiliares para debug
 * -----------------------------
 * N√ÉO s√£o utilizadas na vers√£o final!
 * O seu prop√≥sito √© meramente para debugging.
 *)
let printt (i, c) =
  match c with
  | Null    -> Printf.printf "(%2d, 0); " i
  | Epsilon -> Printf.printf "(%2d, epsilon); " i
  | Char c  -> Printf.printf "(%2d, '%c'); " i c
let printl l =
  Printf.printf "\t[";
  List.iter printt l;
  Printf.printf "]\n"
let printa a =
  Array.iteri (fun i a -> Printf.printf "%2d: " i; printl a) a
let printres s a =
  Printf.printf "%s -> [" s;
  List.iter (Printf.printf "%d; ") a;
  Printf.printf "]\n"
let pausea a =
  let () = printa a in let _ = read_line () in ()


(* Fun√ß√£o:  union
 * --------------
 * Une duas express√µes regulares.
 * Recebe A e B, e devolve A+B.
 *)
let union a b = U (a, b)


(* Fun√ß√£o:  regexp_of_label
 * ------------------------
 * Transforma uma etiqueta de uma transi√ß√£o na express√£o regular correspondente.
 * Utilizado na aplica√ß√£o do algoritmo de McNaughton-Yamada.
 *)
let regexp_of_label = function
| Null    -> V
| Epsilon -> E
| Char c  -> C c


(* Fun√ß√£o:  yamada
 * ---------------
 * Aplica o algoritmo de McNaughton-Yamada para obter a express√£o regular
 * correspondente a um dado aut√≥mato.
 * 
 * L(A) = uni√£o de R(i,f,n+1) para todos os estados iniciais i e finais f, onde:
 * 
 * [1]:
 *   [1.1] -> R(i,j,k+1) = R(i,j,k) + R(i,k,k).R(k,k,k)*.R(k,j,k)
 *   [1.2] -> R(i,j,1) = | i=j -> ∆ê+a | otherwise -> a, where a = {a | (i) -- a --> (f)}
 * 
 * Uma vez que s√≥ existe 1 estado final e existem v√°rios estados finais, a fun√ß√£o
 * mapeia os estados finais com a subfun√ß√£o 'run' e une as express√µes regulares
 * com recurso a um fold_left que aplica a fun√ß√£o 'union'.
 * 
 * 
 * Subfun√ß√£o:  run
 * ---------------
 * Aplica recursivamente a express√£o [1].
 * 
 * 'run i f j' corresponde a R(i,j,k+1) da express√£o [1].
 * Quando j = 1, invoca a subfun√ß√£o 'step', sendo esta a condi√ß√£o de paragem da
 * fun√ß√£o recursiva, uma vez que √© uma transi√ß√£o simples (directo de i a f).
 * 
 * Esta fun√ß√£o recorre √† matriz 'm' de buffering. Se a c√©lula (i,f,k) contiver
 * None, significa que esta c√©lula ainda n√£o foi calculada, pelo que √© preenchida
 * com o resultado (Some <resultado>).
 * √â feita ent√£o a chamada recursiva de 'run i f j', a qual far√° ent√£o a extrac√ß√£o
 * do tipo option recentemente criado.
 * 
 * 
 * Subfun√ß√£o:  step
 * ----------------
 * Verifica todas as transi√ß√µes existentes do estado i ao estado f e devolve
 * a express√£o regular correspondente.
 * 
 * Por exemplo, se de 2 a 4 existirem as transi√ß√µes A e B, ser√° devolvido A+B.
 * N√£o havendo transi√ß√µes, devolve 0 (null).
 *)
let yamada (a : automata) (n : int) (si : int) (sf : int list) m =
  let step i f =
    let t = List.filter (fun (x, _) -> x = f) a.(i) in
      if t = [] then V
      else
        List.fold_left union V <|
        List.map (
          fun (_, l) ->
            let l = regexp_of_label l
            in if i = f then union E l else l
        ) << t
  in
  let rec run i f j =
    if j = 0 then step i f
    else
      let k = j - 1 in
        match m.(i).(f).(k) with
        | None   -> m.(i).(f).(k) <- Some (U (run i f k, P (run i k k, P (S (run k k k), run k f k)))); run i f j
        | Some r -> r
  in
    List.fold_left union V <| List.map (fun f -> run si f n) << sf



(* Fun√ß√£o:  read_final_states
 * --------------------------
 * Traduz uma linha lida do stdin contendo uma string de estados finais numa
 * int list com os referidos estados finais.
 * 
 * Subtrai 1 uma vez que o Array que representa o aut√≥mato √© zero-indexed.
 *)
let read_final_states () =
  List.map (subtract 1 <| int_of_string) <| String.split_on_char ' ' << read_line ()


(* Fun√ß√£o:  read_initial_state
 * ---------------------------
 * Traduz uma linha lida do stdin num int contendo o estado inicial.
 * 
 * Subtrai 1 uma vez que o Array que representa o aut√≥mato √© zero-indexed.
 *)
let read_initial_state () =
  subtract 1 << read_int ()


(* Fun√ß√£o:  read_transition
 * ------------------------
 * Obt√©m do stdin uma string com 3 elementos esperados para formar uma transi√ß√£o:
 *   -> i: estado de partida
 *   -> c: etiqueta de transi√ß√£o
 *   -> j: estado de chegada
 * 
 * A etiqueta √© constru√≠da conforme seja 0, ∆ê ou um caracter c com o tipo de dados
 * 'label' previamente descrito.
 * 
 * Se n√£o forem obtidos exactamente 3 elementos, √© gerado um erro.
 * Se n√£o for poss√≠vel o typecast ou os estados forem out of bounds, √©
 * igualmente gerado um erro.
 * 
 * Subtrai 1 uma vez que o Array que representa o aut√≥mato √© zero-indexed.
 *)
let read_transition (a : automata) =
  match String.split_on_char ' ' (read_line ()) with
  | [i; c; j] -> begin
    try
      let i = subtract 1 << int_of_string i in
      let j = subtract 1 << int_of_string j in
      let c = c.[0] in
        if c = '0' then a.(i) <- (j, Null) :: a.(i)
        else if c = '1' then a.(i) <- (j, Epsilon) :: a.(i)
        else a.(i) <- (j, Char c) :: a.(i)
    with _ -> failwith "read_transition: got 3 but not correct"
  end
  | _ -> failwith "read_transition: not 3 elements"


(* Fun√ß√£o:  read_automata
 * ----------------------
 * L√™ m transi√ß√µes que constituem o aut√≥mato de n estados.
 * 
 * √â gerado um aut√≥mato (Array) inicial com n elementos e √© chamada a fun√ß√£o
 * read_transaction m vezes para obter as m transi√ß√µes do stdin.
 * 
 * A fun√ß√£o auxiliar 'geta' √© encapsulada para poder passar o aut√≥mato em
 * constru√ß√£o por par√¢metro.
 * 
 * O aut√≥mato resultante √© devolvido.
 *)
let read_automata m n =
  let rec geta m a =
    if m = 0 then a
    else (read_transition a; geta (m - 1) a)
  in geta m << init_automata n


(* BLOCO PRINCIPAL (main do programa)
 * ---------------
 * 1. L√™ n, especificando o conjunto S = {1..n}.
 * 2. L√™ i, que identifica que estado de Q √© o estado inicial.
 * 3. L√™ f, cardinalidade do conjunto F dos estados finais.
 * 4. L√™ sf. o conjunto dos estados finais.
 * 5. L√™ m, a cardinalidade de Rùõø.
 * 6. L√™ a, o aut√≥mato com n estados e um total de m transi√ß√µes.
 * 7. Determina a express√£o regular.
 * 8. Simplifica a express√£o regular obtida em 7.
 * 9. Escreve a express√£o regular na forma de uma string formatada.
 *)
let () =
  let n  = read_int () in
  let i  = read_initial_state () in
  let _  = read_int () in
  let sf = read_final_states () in
  let m  = read_int () in
  let a  = read_automata m n in
  let y  = simplify << yamada a n i sf (make_matrix n) in
    Printf.printf "%s\n" << string_of_regexp y


(* EXEMPLO DE EXECU√á√ÉO
 * -------------------
 * Seja dado o seguinte aut√≥mato:
 * 
 *                              + a +
 *                              \  /
 *                              V |
 *   (1) -- a --> (2) -- b --> ([3])
 * 
 * O input correspondente ser√°:
 * 
 *    3
 *    1
 *    1
 *    3
 *    3
 *    1 a 2
 *    2 b 3
 *    3 a 3
 * 
 * Usando as fun√ß√µes de input desenvolvidas, obteremos as seguintes informa√ß√µes
 * acerca do aut√≥mato:
 * 
 *   Estado inicial -> 0
 *   Estados finais -> [2; ]
 *    0:     [( 1, 'a'); ]
 *    1:     [( 2, 'b'); ]
 *    2:     [( 2, 'a'); ]
 * 
 * Nota: como √© usado um Array para representar o aut√≥mato, tudo passa a ser
 * indexado a zero. Os arrays permitem acesso directo e mais c√©lere, da√≠ a sua
 * escolha para esta representa√ß√£o.
 * 
 * A fun√ß√£o 'yamada' √© ent√£o chamada, a qual ir√° determinar que o aut√≥mato ser√°
 * dado por L(A) = R(1,3,4).
 * 
 * R(i,j,k+1) √© calculado pela fun√ß√£o recursiva 'run'. Neste caso, 'run' ser√°
 * invocada com os valores 0, 2 e 3 (devido √† indexa√ß√£o a zero).
 * 
 *   R(0,2,3) = R(0,2,2) + R(0,2,2).R(2,2,2)*.R(2,2,2)
 * 
 * Ora, temos ent√£o que:
 * 
 *   R(0,2,2) = R(0,2,1) + R(0,1,1).R(1,1,1)*.R(1,2,1)
 *   R(2,2,2) = R(2,2,1) + R(2,1,1).R(1,1,1)*.R(1,2,1)
 * 
 * Por sua vez:
 * 
 *   R(0,1,1) = R(0,1,0) + R(0,0,0).R(0,0,0)*.R(0,1,0)
 *   R(0,2,1) = R(0,2,0) + R(0,0,0).R(0,0,0)*.R(0,2,0)
 *   R(1,1,1) = R(1,1,0) + R(1,0,0).R(0,0,0)*.R(0,1,0)
 *   R(1,2,1) = R(1,2,0) + R(1,0,0).R(0,0,0)*.R(0,2,0)
 *   R(2,1,1) = R(2,1,0) + R(2,0,0).R(0,0,0)*.R(0,1,0)
 *   R(2,2,1) = R(2,2,0) + R(2,0,0).R(0,0,0)*.R(0,2,0)
 * 
 * E por fim:
 * 
 *   R(0,0,0) = 0
 *   R(0,1,0) = a
 *   R(0,2,0) = 0
 *   R(1,0,0) = 0
 *   R(1,1,0) = 0
 *   R(1,2,0) = b
 *   R(2,0,0) = 0
 *   R(2,1,0) = 0
 *   R(2,2,0) = ∆ê + a
 * 
 * Portanto:
 * 
 *   R(0,1,1) =       a + 0 . (0* . a) = a
 *   R(0,2,1) =       0 + 0 . (0* . 0) = 0
 *   R(1,1,1) =       0 + 0 . (0* . a) = 0
 *   R(1,2,1) =       b + 0 . (0* . 0) = b
 *   R(2,1,1) =       0 + 0 . (0* . a) = 0
 *   R(2,2,1) = (∆ê + a) + 0 . (0* . 0) = ∆ê + a
 * 
 *   R(0,2,2) =       0 + a . (0* . b) = a . (∆ê . b)
 *   R(2,2,2) = (∆ê + a) + 0 . (0* . b) = ∆ê + a
 * 
 * E assim:
 * 
 *   L(A) = (a . (∆ê . b)) + (a . (∆ê . b)) . ((∆ê + a)* . (∆ê + a))
 * 
 * Ap√≥s simplifica√ß√£o, temos o seguinte output:
 * 
 *   ((a . (1 . b)) + ((a . (1 . b)) . (a* . (1 + a))))
 * 
 * A simplifica√ß√£o apenas ocorre com a express√£o regular final.
 * Contudo, para simplifica√ß√£o do exemplo, fui simplificando logo os casos com 0.
 * 
 * NOTA FINAL:
 * √â usado um mecanismo de buffering que evita o c√°lculo recorrente de R's que j√°
 * foram previamente determinados. Um tipo de dados do tipo option, inserido numa
 * matrix n x n x n+1, alberga esta informa√ß√£o.
 * Uma c√©lula com None signica que ainda n√£o foi calculado.
 * Uma c√©lula com Some r j√° foi calculada, pelo que √© imediatamente devolvido r.
 *)
